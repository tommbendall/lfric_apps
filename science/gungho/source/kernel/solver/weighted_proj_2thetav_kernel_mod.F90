!-----------------------------------------------------------------------------
! (c) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
! Some of the content of this file has been produced with the assistance of
! Met Office GitHub Copilot Enterprise.
!-----------------------------------------------------------------------------
!> @brief Compute the projection operator from the potential temperature space
!!        to the velocity space weighted by the pressure gradient.
!>
!> @details Compute the projection operator \f[<v,\nabla(\Pi)*\gamma>\f]
!!          where v is in W2 and gamma is in the potential temperature space and
!!          exner is computed pointwise from the equation of state.
!!          This is integrated by parts to give
!!          \f[
!!          \left< \nabla.v,\Pi\gamma\right> + \left< v,\Pi\nabla\gamma\right>
!!          \f]
!>
module weighted_proj_2thetav_kernel_mod

  use argument_mod,          only: arg_type, func_type,     &
                                   GH_OPERATOR, GH_FIELD,   &
                                   GH_REAL,                 &
                                   GH_INTEGER, GH_SCALAR,   &
                                   GH_READ, GH_WRITE,       &
                                   ANY_SPACE_9,             &
                                   GH_BASIS, GH_DIFF_BASIS, &
                                   CELL_COLUMN, GH_QUADRATURE_XYoZ
  use constants_mod,         only: r_def, i_def, r_solver
  use fs_continuity_mod,     only: W2, W3
  use kernel_mod,            only: kernel_type

  implicit none

  private

  !---------------------------------------------------------------------------
  ! Public types
  !---------------------------------------------------------------------------

  type, public, extends(kernel_type) :: weighted_proj_2thetav_kernel_type
    private
    type(arg_type) :: meta_args(5) = (/                             &
         arg_type(GH_OPERATOR, GH_REAL, GH_WRITE, W2, ANY_SPACE_9), &
         arg_type(GH_FIELD,    GH_REAL, GH_READ,  W3),              &
         arg_type(GH_FIELD,    GH_REAL, GH_READ,  ANY_SPACE_9),     &
         arg_type(GH_SCALAR,   GH_INTEGER, GH_READ),                &
         arg_type(GH_SCALAR,   GH_INTEGER, GH_READ)                 &
         /)
    type(func_type) :: meta_funcs(3) = (/                           &
         func_type(W2,          GH_BASIS, GH_DIFF_BASIS),           &
         func_type(ANY_SPACE_9, GH_BASIS, GH_DIFF_BASIS),           &
         func_type(W3,          GH_BASIS)                           &
         /)
    integer :: operates_on = CELL_COLUMN
    integer :: gh_shape = GH_QUADRATURE_XYoZ
  contains
    procedure, nopass :: weighted_proj_2thetav_code
  end type

  !---------------------------------------------------------------------------
  ! Contained functions/subroutines
  !---------------------------------------------------------------------------
  public :: weighted_proj_2thetav_code

contains

!> @brief Compute the weighted projection from Wtheta to W2
!! @param[in] cell Cell number
!! @param[in] nlayers Number of layers
!! @param[in] ncell_3d ncell*ndf
!! @param[in,out] projection Projection operator to compute
!! @param[in] exner Exner pressure
!! @param[in] moist_dyn_factor The moist dynamics factor for theta
!! @param[in] element_order_h Horizontal element order of the function space
!! @param[in] element_order_v Vertical element order of the function space
!! @param[in] ndf_w2 Number of degrees of freedom per cell
!! @param[in] basis_w2 Basis functions evaluated at quadrature points
!! @param[in] diff_basis_w2 Differential vector basis functions evaluated
!!                          at quadrature points
!! @param[in] ndf_wtheta Number of degrees of freedom per cell
!! @param[in] undf_wtheta Number of unique degrees of freedom for Wtheta
!! @param[in] map_wtheta Dofmap for Wtheta
!! @param[in] basis_wtheta Basis functions evaluated at quadrature points
!! @param[in] diff_basis_wtheta Differential vector basis functions
!!                              evaluated at quadrature points
!! @param[in] ndf_w3 Number of degrees of freedom per cell
!! @param[in] undf_w3 Total number of degrees of freedom
!! @param[in] map_w3 Dofmap at the base of the column
!! @param[in] basis_w3 Basis functions evaluated at quadrature points
!! @param[in] nqp_h Number of horizontal quadrature points
!! @param[in] nqp_v Number of vertical quadrature points
!! @param[in] wqp_h Horizontal quadrature weights
!! @param[in] wqp_v Vertical quadrature weights
subroutine weighted_proj_2thetav_code(cell, nlayers, ncell_3d,             &
                                      projection,                          &
                                      exner,                               &
                                      moist_dyn_factor,                    &
                                      element_order_h, element_order_v,    &
                                      ndf_w2, basis_w2, diff_basis_w2,     &
                                      ndf_wtheta, undf_wtheta, map_wtheta, &
                                      basis_wtheta, diff_basis_wtheta,     &
                                      ndf_w3, undf_w3, map_w3, basis_w3,   &
                                      nqp_h, nqp_v, wqp_h, wqp_v)

  implicit none

  ! Arguments
  integer(kind=i_def),                        intent(in) :: cell, nqp_h, nqp_v
  integer(kind=i_def),                        intent(in) :: nlayers
  integer(kind=i_def),                        intent(in) :: element_order_h
  integer(kind=i_def),                        intent(in) :: element_order_v
  integer(kind=i_def),                        intent(in) :: ncell_3d
  integer(kind=i_def),                        intent(in) :: undf_w3, ndf_w3, ndf_w2
  integer(kind=i_def),                        intent(in) :: undf_wtheta, ndf_wtheta
  integer(kind=i_def), dimension(ndf_w3),     intent(in) :: map_w3
  integer(kind=i_def), dimension(ndf_wtheta), intent(in) :: map_wtheta

  real(kind=r_def), dimension(1,ndf_w3,nqp_h,nqp_v),     intent(in) :: basis_w3
  real(kind=r_def), dimension(1,ndf_w2,nqp_h,nqp_v),     intent(in) :: diff_basis_w2
  real(kind=r_def), dimension(3,ndf_w2,nqp_h,nqp_v),     intent(in) :: basis_w2
  real(kind=r_def), dimension(1,ndf_wtheta,nqp_h,nqp_v), intent(in) :: basis_wtheta
  real(kind=r_def), dimension(3,ndf_wtheta,nqp_h,nqp_v), intent(in) :: diff_basis_wtheta

  real(kind=r_solver), dimension(ncell_3d,ndf_w2,ndf_wtheta), intent(inout) :: projection
  real(kind=r_solver), dimension(undf_w3),                    intent(in)    :: exner
  real(kind=r_solver), dimension(undf_wtheta),                intent(in)    :: moist_dyn_factor

  real(kind=r_def), dimension(nqp_h), intent(in) :: wqp_h
  real(kind=r_def), dimension(nqp_v), intent(in) :: wqp_v

  ! Internal variables
  integer(kind=i_def)                  :: df, df0, df2, k, ik
  integer(kind=i_def)                  :: qp1, qp2
  integer(kind=i_def)                  :: k_h, k_v
  integer(kind=i_def)                  :: ndf_w2h, ndf_w2h_vol, ndf_w2v_vol, &
                                          ndf_w2_vol
  real(kind=r_solver), dimension(ndf_w3)  :: exner_e
  real(kind=r_solver)                     :: integrand
  real(kind=r_solver)                     :: div_gamma_v
  real(kind=r_solver)                     :: exner_quad
  real(kind=r_solver)                     :: wt

  real(kind=r_solver), dimension(1,ndf_w3,nqp_h,nqp_v)     :: rsol_basis_w3
  real(kind=r_solver), dimension(1,ndf_w2,nqp_h,nqp_v)     :: rsol_diff_basis_w2
  real(kind=r_solver), dimension(3,ndf_w2,nqp_h,nqp_v)     :: rsol_basis_w2
  real(kind=r_solver), dimension(1,ndf_wtheta,nqp_h,nqp_v) :: rsol_basis_wtheta
  real(kind=r_solver), dimension(3,ndf_wtheta,nqp_h,nqp_v) :: rsol_diff_basis_wtheta

  rsol_basis_w3          = real(basis_w3, r_solver)
  rsol_diff_basis_w2     = real(diff_basis_w2, r_solver)
  rsol_basis_w2          = real(basis_w2, r_solver)
  rsol_basis_wtheta      = real(basis_wtheta, r_solver)
  rsol_diff_basis_wtheta = real(diff_basis_wtheta, r_solver)

  k_h = element_order_h
  k_v = element_order_v

  ! Calculate numbers of vertical and horizontal dofs of W2 for use in looping
  ! over vertical dofs
  ndf_w2h     = 2*(k_h + 1)*(k_h + 2)*(k_v + 1)
  ndf_w2h_vol = 2*k_h*(k_h + 1)*(k_v + 1)
  ndf_w2v_vol = (k_h + 1)*(k_h + 1)*k_v
  ndf_w2_vol  = ndf_w2h_vol + ndf_w2v_vol

  do k = 0, nlayers - 1
    ik = k + 1 + (cell-1)*nlayers
    do df = 1,ndf_w3
      exner_e(df) = exner(map_w3(df) + k)
    end do
    projection(ik,:,:) = 0.0_r_solver
    do qp2 = 1, nqp_v
      do qp1 = 1, nqp_h
        exner_quad = 0.0_r_solver
        do df = 1, ndf_w3
          exner_quad = exner_quad &
                     + exner_e(df)*rsol_basis_w3(1,df,qp1,qp2)
        end do
        wt = real(wqp_h(qp1)*wqp_v(qp2), r_solver)
        integrand = exner_quad*wt
        do df0 = 1, ndf_wtheta
          do df2 = ndf_w2h_vol+1, ndf_w2
            ! W2 dofs are ordered:
            !   a) Horizontal volume dofs
            !   b) Vertical volume dofs
            !   c) Horizontal face dofs
            !   d) Vertical face dofs
            ! Only use the vertical dofs of W2, which satisfy one of two
            ! conditions:
            !   b) df2 is a vertical volume dof of W2 (higher order spaces
            !      only), so
            !        ndf_w2h_vol < df2 <= ndf_w2_vol
            !   d) df2 is a vertical face dof of W2 (lives on the top or
            !      bottom face of a cell), so
            !        ndf_w2h + ndf_w2v_vol < df2
            if ( df2 <= ndf_w2_vol .or. ndf_w2h + ndf_w2v_vol < df2 ) then
              div_gamma_v = rsol_diff_basis_w2(1,df2,qp1,qp2)         &
                            *rsol_basis_wtheta(1,df0,qp1,qp2)         &
                          + dot_product(rsol_basis_w2(:,df2,qp1,qp2), &
                                        rsol_diff_basis_wtheta(:,df0,qp1,qp2))
              projection(ik,df2,df0) = projection(ik,df2,df0) &
                                     + integrand*div_gamma_v &
                                       *moist_dyn_factor(map_wtheta(df0)+k)
            end if
          end do
        end do
      end do
    end do
  end do
end subroutine weighted_proj_2thetav_code

end module weighted_proj_2thetav_kernel_mod
